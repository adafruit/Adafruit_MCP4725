/**************************************************************************/
/*!
    @file     setactualvoltage.ino
    @author   Alex Mitchell (MitchellSoft Technology Ltd.)
    @license  BSD (see license.txt)

    This example shows how to set a voltage on the MCP4725 by specifying
    the output voltage in millivolts, rather than calculating the raw output
    value required by the MCP4725.

    The MCP4725 has a resolution of 12 bits (4096 steps, from 0 to 4095), so 
    it can get close, but not always output exactly the voltage requested.
    In case this matters in your application, setNearestActualVoltage() returns
    the value of the nearest voltage that could be generated by the MCP4725
    so that your code can handle any discrepancies.

    The reference voltage must be known and set for this to work.  The
    reference voltage will often be 5V (5000mV), but especially when
    running from USB power, it may be slightly off 5V.
    Any difference between the reference voltage and the value specified
    will create an inaccuracy in the voltage that the MCP4725 outputs.

    This is an example sketch for the Adafruit MCP4725 breakout board
    ----> http://www.adafruit.com/products/935

    Adafruit invests time and resources providing this open source code,
    please support Adafruit and open-source hardware by purchasing
    products from Adafruit!
*/
/**************************************************************************/
#include <Wire.h>
#include <Adafruit_MCP4725.h>

static const int REFERENCE_VOLTAGE_mV = 5000;
static const bool WRITE_EEPROM = false;

Adafruit_MCP4725 dac;

void setup(void) {
  Serial.begin(9600);
  Serial.println("Hello!");

  // For Adafruit MCP4725A1 the address is 0x62 (default) or 0x63 (ADDR pin tied to VCC)
  // For MCP4725A0 the address is 0x60 or 0x61
  // For MCP4725A2 the address is 0x64 or 0x65
  dac.begin(0x62);
    
  Serial.println("Example sketch demonstrating how to set a voltage with the MCP4725");
}

void loop(void) {
  Serial.println("Outputting 0V");
  dac.setNearestActualVoltage(0, REFERENCE_VOLTAGE_mV, WRITE_EEPROM);
  delay(5000);
  
  Serial.println("Requesting 2500mV (2.5V)");
  float nearestMillivoltsPossible = dac.setNearestActualVoltage(2500, REFERENCE_VOLTAGE_mV, WRITE_EEPROM);
  Serial.print("Nearest value to 2500mV that the MCP4725 could output is: ");
  Serial.print(nearestMillivoltsPossible);
  Serial.println("mV");
  delay(5000);
  
  Serial.println("Outputting 5000mV (5V)");
  dac.setNearestActualVoltage(5000, REFERENCE_VOLTAGE_mV, WRITE_EEPROM);
  delay(5000);
  Serial.println("******");
}